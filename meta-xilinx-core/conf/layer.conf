# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have a packages directory, add to BBFILES
BSP_XILINX_DERIVED ??= ""
BBFILES += "${@bb.utils.contains_any('MACHINE', '${BSP_XILINX_DERIVED}', '${LAYERDIR}/recipes-*/*/*.bb', '', d)}"
BBFILES += "${@bb.utils.contains_any('MACHINE', '${BSP_XILINX_DERIVED}', '${LAYERDIR}/recipes-*/*/*.bbappend', '', d)}"

BBFILE_COLLECTIONS += "xilinx"
BBFILE_PATTERN_xilinx = "^${LAYERDIR}/"
BBFILE_PRIORITY_xilinx = "5"

BBFILES_DYNAMIC += " \
openembedded-layer:${LAYERDIR}/dynamic-layers/openembedded-layer/recipes-*/*/*.bb \
openembedded-layer:${LAYERDIR}/dynamic-layers/openembedded-layer/recipes-*/*/*.bbappend \
meta-python:${LAYERDIR}/dynamic-layers/meta-python/recipes-*/*/*.bb \
meta-python:${LAYERDIR}/dynamic-layers/meta-python/recipes-*/*/*.bbappend \
openamp-layer:${LAYERDIR}/dynamic-layers/openamp-layer/recipes-*/*/*.bb \
openamp-layer:${LAYERDIR}/dynamic-layers/openamp-layer/recipes-*/*/*.bbappend \
virtualization-layer:${LAYERDIR}/dynamic-layers/virtualization-layer/recipes-*/*/*.bb \
virtualization-layer:${LAYERDIR}/dynamic-layers/virtualization-layer/recipes-*/*/*.bbappend \
xilinx-tools:${LAYERDIR}/dynamic-layers/meta-xilinx-tools/recipes-*/*/*.bb \
xilinx-tools:${LAYERDIR}/dynamic-layers/meta-xilinx-tools/recipes-*/*/*.bbappend \
"

LAYERDEPENDS_xilinx = "core"
LAYERRECOMMENDS_xilinx = "openembedded-layer"

LAYERSERIES_COMPAT_xilinx = "scarthgap"

SIGGEN_EXCLUDE_SAFE_RECIPE_DEPS += " \
  *->xrt \
  *->zocl \
  *->cairo \
  *->libepoxy \
  *->gstreamer1.0-plugins-base \
  *->gtk+ \
  *->gtk+3 \
  *->libglu \
  *->libsdl \
  *->libsdl2 \
  *->qemu \
  *->xserver-xorg \
"

XILINX_RELEASE_VERSION ??= "v2023.2"

BUILDCFG_VARS:append = " SOC_VARIANT XILINX_RELEASE_VERSION"

XILINX_QEMU_VERSION[v2022.1] = "v7.1.0-xilinx-v2022.1%"
XILINX_QEMU_VERSION[v2022.2] = "v7.1.0-xilinx-v2022.2%"
XILINX_QEMU_VERSION[v2023.1] = "v7.1.0-xilinx-v2023.1%"
XILINX_QEMU_VERSION[v2023.2] = "v7.1.0-xilinx-v2023.2%"
PREFERRED_VERSION_qemu-xilinx ?= "${@d.getVarFlag('XILINX_QEMU_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"
PREFERRED_VERSION_qemu-xilinx-native ?= "${@d.getVarFlag('XILINX_QEMU_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"
PREFERRED_VERSION_qemu-xilinx-system-native ?= "${@d.getVarFlag('XILINX_QEMU_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"
PREFERRED_VERSION_qemu-devicetrees ?= "xilinx-${XILINX_RELEASE_VERSION}%"

DEFAULT_XILINX_QEMU = "qemu-xilinx"
DEFAULT_XILINX_QEMU:arm = "qemu"
PREFERRED_PROVIDER_qemu ?= "${DEFAULT_XILINX_QEMU}"

XILINX_ATF_VERSION[v2022.1] = "2.6-xilinx-v2022.1%"
XILINX_ATF_VERSION[v2022.2] = "2.6-xilinx-v2022.2%"
XILINX_ATF_VERSION[v2023.1] = "2.8-xilinx-v2023.1%"
XILINX_ATF_VERSION[v2023.2] = "2.8-xilinx-v2023.2%"
PREFERRED_VERSION_arm-trusted-firmware ?= "${@d.getVarFlag('XILINX_ATF_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"

XILINX_UBOOT_VERSION[v2022.1] = "1:v2021.01-xilinx-v2022.1%"
XILINX_UBOOT_VERSION[v2022.2] = "1:v2022.01-xilinx-v2022.2%"
XILINX_UBOOT_VERSION[v2023.1] = "1:v2023.01-xilinx-v2023.1%"
XILINX_UBOOT_VERSION[v2023.2] = "1:v2023.01-xilinx-v2023.2%"

PREFERRED_VERSION_u-boot-xlnx ?= "${@d.getVarFlag('XILINX_UBOOT_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"
PREFERRED_VERSION_u-boot-tools-xlnx ?= "${@d.getVarFlag('XILINX_UBOOT_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"

XILINX_LINUX_VERSION[v2022.1] = "5.15.19-xilinx-v2022.1%"
XILINX_LINUX_VERSION[v2022.2] = "5.15.36-xilinx-v2022.2%"
XILINX_LINUX_VERSION[v2023.1] = "6.1.30-xilinx-v2023.1%"
XILINX_LINUX_VERSION[v2023.2] = "6.1.60-xilinx-v2023.2%"
PREFERRED_VERSION_linux-xlnx ?= "${@d.getVarFlag('XILINX_LINUX_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"

# Mali needs to match the kernel version
PREFERRED_VERSION_kernel-module-mali = "r9p0-01rel0-${XILINX_RELEASE_VERSION}"

# DP kernel module
KERNEL_MODULE_DP_VERSION[v2022.1] = "5.10.0+xilinx-v2022.1+git%"
KERNEL_MODULE_DP_VERSION[v2022.2] = "5.10.0+xilinx-v2022.2+git%"
KERNEL_MODULE_DP_VERSION[v2023.1] = "6.1.0+xilinx-v2023.1+git%"
KERNEL_MODULE_DP_VERSION[v2023.2] = "6.1.0+xilinx-v2023.2+git%"
PREFERRED_VERSION_kernel-module-dp ?= "${@d.getVarFlag('KERNEL_MODULE_DP_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"

# HDMI kernel module
KERNEL_MODULE_HDMI_VERSION[v2022.1] = "5.15.0+xilinx-v2022.1+git%"
KERNEL_MODULE_HDMI_VERSION[v2022.2] = "5.15.19+xilinx-v2022.2+git%"
KERNEL_MODULE_HDMI_VERSION[v2023.1] = "6.1+xilinx-v2023.1+git%"
KERNEL_MODULE_HDMI_VERSION[v2023.2] = "6.1+xilinx-v2023.2+git%"
PREFERRED_VERSION_kernel-module-hdmi ?= "${@d.getVarFlag('KERNEL_MODULE_HDMI_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"

# VCU kernel module
PREFERRED_VERSION_kernel-module-vcu = "1.0.0-xilinx-${XILINX_RELEASE_VERSION}+git%"

# VDU kernel module
PREFERRED_VERSION_kernel-module-vdu = "1.0.0-xilinx-${XILINX_RELEASE_VERSION}+git%"

# XRT/ZOCL
XRT_ZOCL_VERSION[v2022.1] = "202210.2.13.479"
XRT_ZOCL_VERSION[v2022.2] = "202220.2.14.0"
XRT_ZOCL_VERSION[v2023.1] = "202310.2.15.0"
XRT_ZOCL_VERSION[v2023.2] = "202320.2.16.0"
PREFERRED_VERSION_xrt ?= "${@d.getVarFlag('XRT_ZOCL_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"
PREFERRED_VERSION_zocl ?= "${@d.getVarFlag('XRT_ZOCL_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"

# AI-Engine
AIEFAL_VERSION[v2022.1] = "1.4"
AIEFAL_VERSION[v2022.2] = "1.4"
AIEFAL_VERSION[v2023.1] = "1.5"
AIEFAL_VERSION[v2023.2] = "1.5"
PREFERRED_VERSION_aiefal ?= "${@d.getVarFlag('AIEFAL_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"

AI_ENGINE_DRIVER_VERSION[v2022.1] = "3.3"
AI_ENGINE_DRIVER_VERSION[v2022.2] = "3.3"
AI_ENGINE_DRIVER_VERSION[v2023.1] = "3.4"
AI_ENGINE_DRIVER_VERSION[v2023.2] = "3.4"
PREFERRED_VERSION_ai-engine-driver ?= "${@d.getVarFlag('AI_ENGINE_DRIVER_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"

# Add support to eSDK for gen-machine-conf if it exists
PLNX_SCRIPTS_PATH = "${LAYERDIR}/gen-machine-conf/gen-machine-scripts"
BB_HASHEXCLUDE_COMMON:append = " PLNX_SCRIPTS_PATH"

IMAGE_CLASSES += "gen-machine-conf"

BBMASK += "${LAYERDIR}/recipes-devtools/qemu/"
BBMASK += "${LAYERDIR}/dynamic-layers/virtualization-layer/recipes-devtools/qemu/"
BBMASK += "${LAYERDIR}/recipes-kernel/linux/"
BBMASK += "${@bb.utils.contains_any('MACHINE_FEATURES', 'mali400', '', '${LAYERDIR}/recipes-graphics/', d)}"
BBMASK += "${@bb.utils.contains_any('MACHINE_FEATURES', 'mali400', '', '${LAYERDIR}/recipes-gnome/', d)}"
BBMASK += "${@bb.utils.contains_any('MACHINE_FEATURES', 'mali400', '', '${LAYERDIR}/recipes-multimedia/gstreamer/', d)}"
