# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have recipes-* directories, add to BBFILES
BSP_XILINX_DERIVED ??= ""
BBFILES += "${@bb.utils.contains_any('MACHINE', '${BSP_XILINX_DERIVED}', '${LAYERDIR}/recipes-*/*/*.bb', '', d)}"
BBFILES += "${@bb.utils.contains_any('MACHINE', '${BSP_XILINX_DERIVED}', '${LAYERDIR}/recipes-*/*/*.bbappend', '', d)}"
BBFILES += "${@bb.utils.contains_any('MACHINE', '${BSP_XILINX_DERIVED}', '${LAYERDIR}/recipes-*/*.bb', '', d)}"
BBFILES += "${@bb.utils.contains_any('MACHINE', '${BSP_XILINX_DERIVED}', '${LAYERDIR}/recipes-*/*.bbappend', '', d)}"

BBFILE_COLLECTIONS += "xilinx-standalone"
BBFILE_PATTERN_xilinx-standalone = "^${LAYERDIR}/"
BBFILE_PRIORITY_xilinx-standalone = "7"

LAYERDEPENDS_xilinx-standalone = "core xilinx"

LAYERSERIES_COMPAT_xilinx-standalone = "scarthgap"

# Set this to enable this layer
#XILINX_WITH_ESW = "generic"

# Sanity check for meta-xilinx-standalone layer.
# Setting SKIP_META_XILINX_STANDALONE_SANITY_CHECK to "1" would skip the bbappend files check.
INHERIT += "sanity-meta-xilinx-standalone"

# We need to load the ESW and related config components, only if XILINX_WITH_ESW
# is in defined in some way.  Since we don't know the configuration during
# layer.conf load time, we delay using a special bbclass that simply includes
# the META_XILINX_STANDLONE_CONFIG_PATH file.
META_XILINX_STANDALONE_CONFIG_PATH = "${LAYERDIR}/conf/distro/include/meta-xilinx-standalone-default-versions.inc"

USER_CLASSES:append = " meta-xilinx-standalone-cfg"
